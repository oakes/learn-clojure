## Loops Continued

So far, we've made a function that tells us the type of a given character:

```clojure
(def digits #{\1 \2 \3 \4 \5 \6 \7 \8 \9 \0})
(def char-names {\space :space
                 \. :period
                 \/ :forward-slash
                 \" :double-quote})

(defn char-type [ch]
  (cond
    (contains? digits ch)
    :digit

    (contains? char-names ch)
    (get char-names ch)

    true
    :other))
```

Then, we looped over an array of characters and grouped them according to their type:

```clojure
(def chars (vec "42 0.5 1/2 foo-bar"))

(loop [result []
       index 0
       last-type nil
       group nil]
  (let [ch (get chars index)
        new-type (char-type ch)
        new-index (+ index 1)]
    (if ch
      (if (not= last-type new-type)
        (let [new-result (if group
                           (conj result group)
                           result)]
          (recur new-result new-index new-type [ch]))
        (recur result new-index last-type (conj group ch)))
      (conj result group))))
```

We now want to combine these grouped characters into strings. One way to do that is with the `str` function:

```clojure
(str \h \e \l \l \o)
```

So now we can change our loop to store the groups as strings rather than in vectors by simply changing how the group is passed to `recur`:

```clojure
(loop [result []
       index 0
       last-type nil
       group nil]
  (let [ch (get chars index)
        new-type (char-type ch)
        new-index (+ index 1)]
    (if ch
      (if (not= last-type new-type)
        (let [new-result (if group
                           (conj result group)
                           result)]
          (recur new-result new-index new-type (str ch)))
        (recur result new-index last-type (str group ch)))
      (conj result group))))
```

While this is a good start, it's not quite what we want. A reader needs to treat `1.5` as a discrete number, so instead of `["0" "." "5"]` we really just want `"0.5"`. To do this, let's make a more sophisticated function for deciding if the types are different:

```clojure
(defn diff-type? [left-type right-type]
  (and (not= left-type right-type)
       (not= [left-type right-type]
             [:digit :period])))
```

Then use it where we are currently are doing `(not= last-type new-type)`:

```clojure
(loop [result []
       index 0
       last-type nil
       group nil]
  (let [ch (get chars index)
        new-type (char-type ch)
        new-index (+ index 1)]
    (if ch
      (if (diff-type? last-type new-type)
        (let [new-result (if group
                           (conj result group)
                           result)]
          (recur new-result new-index new-type (str ch)))
        (recur result new-index last-type (str group ch)))
      (conj result group))))
```

Now our `0.5` is merged together! Let's finally put this loop in a function so we can easily call it:

```clojure
(defn partition-chars [chars]
  (loop [result []
         index 0
         last-type nil
         group nil]
    (let [ch (get chars index)
          new-type (char-type ch)
          new-index (+ index 1)]
      (if ch
        (if (diff-type? last-type new-type)
          (let [new-result (if group
                             (conj result group)
                             result)]
            (recur new-result new-index new-type (str ch)))
          (recur result new-index last-type (str group ch)))
        (conj result group)))))
```

Now we can call it like this:

```clojure
(partition-chars chars)
```

Notice that the function argument is called `chars`. It is perfectly fine for a var and a function argument to have the same name. Inside the function, the evaluator will resolve `chars` as the argument. Outside the function, `chars` will resolve to the var as usual. This is called shadowing.

Now we can easily start merging other things. Let's make fractions merge together:

```clojure
(defn diff-type? [left-type right-type]
  (and (not= left-type right-type)
       (not= [left-type right-type]
             [:digit :period])
       (not= [left-type right-type]
             [:digit :forward-slash])))
```

And now `1/2` is a single string:

```clojure
(partition-chars chars)
```

There's a simpler way to write this. We can put all "merge pairs" in a set:

```clojure
(def merge-pairs #{[:digit :period]
                   [:digit :forward-slash]})
```

And then make `diff-type?` check if the current pair is in it:

```clojure
(defn diff-type? [left-type right-type]
  (and (not= left-type right-type)
       (not (contains? merge-pairs [left-type right-type]))))
```

It should still work the same way:

```clojure
(partition-chars chars)
```
